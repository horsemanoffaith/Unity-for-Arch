# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Michael Healy <horsemanoffaith@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety

pkgname=indicator-keyboard
_actual_ver=0.0.0
_extra_ver=+16.10.20160808
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Indicator for switching keyboard layouts and input methods"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-keyboard"
license=(GPL)
groups=(unity)
depends=(accountsservice fcitx gnome-desktop gucharmap ibus libgee libgnomekbd
         lightdm-ubuntu unity-control-center)
makedepends=(gobject-introspection intltool vala)
conflicts=(ibus-ubuntu)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-keyboard_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-vala-build-fix.patch
        0002-Remove-code-depending-on-accountsservices-patch.patch
        0003-Remove-unavailable-DBus-method-activate_input_source.patch
        0004-Remove-unavailable-DBus-method-xkeyboard-layouts.patch
        0005-Spawn-ibus-daemon-at-startup.patch
	0006-Not-a-phone.patch)

sha512sums=('cdfb66b313a0045e9aeba1ce912ea92e5eff7480d38dfb12b16a23b82857beb3662fd55ce23ba595a91385c0d14199bf1d7eab010773e330e381f9b30e7a67c1'
            'ad25fafbe879e65ebd22c7fe77ca4504b342c1baab7039df0b563e96799e0cf2d8dbc548628155621a74e07e58b065dd7f588f5d6edb1476e80c070f60a527a9'
            'df5ab3b8749dc152bc7d9a185be7421901598fdfaae660a8e13fe8f4a6e09d7141d94a1c5ed8b53347c45060607131fa33a5f04a54461b3d7b2656a1022d9030'
            'f334727da7dc3149213e04ef7a9aed80c48d8de22dff4cc6ec6bf7f7a6d9f3473d9cb482b6d97a6ae73e8919723a556e012558b17c0b0a6148c86775427b8d53'
            '57242ae67d7be747133b9f40c85c629c8c1dacf8cb4f705d2d794ea0c799b18d2d87ccea46ed2e4116c7e6aafe2f590fddcddfd4c42121f12a9683c3c9442626'
            '308780ed076d9c06a826178a09accb3b36c19b8969dd0475962c4f8c0a45322b6fc23d2eee5a205d6cc4a899eb7e645161c9ef613549d3443c371a2cad17229d'
            'e21d11893c37eba63f081efe0f0d4b006524477d6eb061183056eb31defd8f44fa3027c8f0e7c94d2a9b0b5e2e7cda88b10043ad4ac436919e9c5e80df713fdf')

prepare() {
    patch -p1 -i ../0001-vala-build-fix.patch
    patch -p1 -i ../0002-Remove-code-depending-on-accountsservices-patch.patch
    patch -p1 -i ../0003-Remove-unavailable-DBus-method-activate_input_source.patch
    patch -p1 -i ../0004-Remove-unavailable-DBus-method-xkeyboard-layouts.patch
    patch -p1 -i ../0005-Spawn-ibus-daemon-at-startup.patch
    patch -p1 -i ../0006-Not-a-phone.patch
}

build() {
    intltoolize -f
    autoreconf -vfi
    ./configure --prefix=/usr --libexecdir=/usr/lib
    make
}

package() {
    make DESTDIR="${pkgdir}/" install
}
