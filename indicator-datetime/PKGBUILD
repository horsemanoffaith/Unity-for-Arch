# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-datetime
_actual_ver=15.10
_extra_ver=+17.04.20170210
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A very, very simple clock"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-datetime"
license=(GPL)
depends=(evolution-data-server gsettings-ubuntu-schemas gstreamer indicator-messages
         libaccounts-glib libtimezonemap properties-cpp unity-control-center)
makedepends=(intltool cmake)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
         0001-Not-a-phone.patch
         0002-Disable-tests.patch)
sha512sums=('d475424371ba6e581daf90d9a0b4f2e84d8b3725ef0a0bae7af7e811a90df6c757872de499708ecff88049a495c0c056c12530557bdb9bf45777ed673ed65374'
            '021232098fb76f7e41468aebd8e950099b58c66a673ab332fa379d108348ac5e7472b3355bebd1e7bd77a6ed31af790f7ad105fd67648f2e418e9ae53ee53940'
            '7748c8787a3868de8faaa19db7b281aec6e7c2686841473e55fb9b52d0ada78b8a2a3f3e87127fcaaaab1360b13deb917b165d03c332f9f11c2f4c9c650d1572')

prepare() {
    
    patch -p1 -i 0001-Not-a-phone.patch
    patch -p1 -i 0002-Disable-tests.patch
        
}

build() {

    export CFLAGS+=" -Wno-error"

    mkdir -p build
    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
        -Denable_tests=OFF

    make
}

package() {

    cd build

    make DESTDIR="${pkgdir}/" install

    # Use language packs
    rm -r "${pkgdir}/usr/share/locale/"

    rm -rv "${pkgdir}"/usr/share/upstart
}
