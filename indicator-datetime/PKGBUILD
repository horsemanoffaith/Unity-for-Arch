# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-datetime
_actual_ver=15.10
_extra_ver=+17.04.20161201
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A very, very simple clock"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-datetime"
license=(GPL)
depends=(evolution-data-server gsettings-ubuntu-schemas gstreamer indicator-messages
         libaccounts-glib libtimezonemap properties-cpp unity-control-center)
makedepends=(intltool cmake)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Not-a-phone.patch)
sha512sums=('1f94bdcebe71cd5b4388c0ff2c50a14285b6c9da28b7439a1ea8ad187a728f7ae9f61d6995a2e3dd1c4cf9e4c15f02c6029a8c5dc74015ec08bace42b4995f99'
            '1d1d7c6d50222fe972e61437f2c3dbfd2dfde1baf36e063e1514ae8afb3aa5c4e02562c0c13cbe8a8b6ce94abe5a0901b2bbbe64c3d0be042b1dbe6931ddd886')

prepare() {
    
    patch -p1 -i 0001-Not-a-phone.patch
    
}

build() {

    export CFLAGS+=" -Wno-error"

    mkdir -p build
    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
        -Denable_tests=OFF

    make
}

package() {

    cd build

    make DESTDIR="${pkgdir}/" install

    # Use language packs
    rm -r "${pkgdir}/usr/share/locale/"

    rm -rv "${pkgdir}"/usr/share/upstart
}
