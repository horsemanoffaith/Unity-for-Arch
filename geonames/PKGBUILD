# Maintainer: Michael Healy <horsemanoffaith@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=geonames
_actual_ver=0.2
_extra_ver=+16.04.20160321
_ubuntu_ver=0ubuntu1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="Parse and query the geonames database dump"
arch=(i686 x86_64)
url="https://launchpad.net/geonames"
license=(LGPL)
makedepends=(pkg-config glib2)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/geonames_${_actual_ver}${_extra_ver}-${_ubuntu_ver}.tar.xz"
	remove-gtk-doc-make.patch)
sha512sums=('70e5ee6926405b4d45aa3479e4de78a9200aea5c17ebe64717bb5be06c4dfd390875e0952e067db4f9e4c792d65e4e96494eb9e2fe294fd59bcfd3ea8b0df8eb'
            '57a40be842d780db48e48ab61468cfe6019282da999613295917afe371daf0e2e704f5cf6ff8ecfd3547dd105fed8bd8f1257ca7442017d7441b7edb514e3f73')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  # Remove unnecessary gtk-doc.make 
  patch -p1 -i ../remove-gtk-doc-make.patch

}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

    autoreconf -vfi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}
