# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Michael Healy <horsemanoffaith@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=zesty
# vercheck-launchpad: name=${pkgname}

pkgname=overlay-scrollbar
_actual_ver=0.2.17.1
_extra_ver=+16.04.20151117
pkgver=${_actual_ver}${_extra_ver//\+/.}
pkgrel=2
pkgdesc="Overlaid Scrollbars that take up no active screen real-estate"
arch=(i686 x86_64)
url="https://launchpad.net/ayatana-scrollbar"
license=(LGPL)
groups=(unity)
depends=(dconf gtk2-ubuntu)
conflicts=(liboverlay-scrollbar liboverlay-scrollbar3)
replaces=(liboverlay-scrollbar liboverlay-scrollbar3)
options=(!emptydirs)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/overlay-scrollbar_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('0e718acb39307c6aa92747eb7b353a8468c358c5a25525ae2df830a4b3cb1c1bf7a3ee03410330db12c253be2d09211603cfc7274e67fa19917463b57eda899b')

build() {
    cd "${pkgbase}-${_actual_ver}${_extra_ver}"

    autoreconf -vfi
    ./configure --prefix=/usr --sysconfdir=/etc --disable-static
    make
}

package() {
    cd "${pkgbase}-${_actual_ver}${_extra_ver}"
    make DESTDIR="${pkgdir}/" install

    # Put X11 startup script in correct directory
    install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
    mv "${pkgdir}/etc/X11/Xsession.d/81overlay-scrollbar" \
        "${pkgdir}/etc/X11/xinit/xinitrc.d/"
    chmod 755 "${pkgdir}/etc/X11/xinit/xinitrc.d/81overlay-scrollbar"
}
