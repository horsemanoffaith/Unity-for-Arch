# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase%-*}, repo=zesty
# vercheck-archlinux: name=${pkgbase%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgbase%-*}, majorver=1.2

pkgbase=network-manager-applet-ubuntu
pkgname=(nm-connection-editor-ubuntu network-manager-applet-ubuntu)
_ubuntu_rel=1ubuntu2
pkgver=1.4.2
pkgrel=2
pkgdesc="Applet for managing network connections"
arch=(i686 x86_64)
url="http://www.gnome.org/projects/NetworkManager/"
license=(GPL2 LGPL2.1)
depends=(libnm-glib libgudev gtk3-ubuntu mobile-broadband-provider-info iso-codes
         libappindicator-gtk3-ubuntu python2)
makedepends=(libsecret libnotify libmm-glib intltool gobject-introspection)
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgbase%-*}/${pkgver%.*}/${pkgbase%-*}-${pkgver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase%-*}_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
sha512sums=('fedd1b41a1b5d63c1647f6801552ecb9676f93e88833dea7bf3e1dc43950c904d5deabdfefa50c46799b3e18297f9349f4462aa9531066482d78b30781e15b60'
            'd0e77216ea72075c638b4ee7b372177eb981d846de780eafcd3a8fc8121db4a4fda3eeeacefd36511d7a00d702a84a64a46d5040837f0f9230b0b85e945555eb')

prepare() {
    mkdir -p nma/etc/xdg nma/usr/bin \
        nma/usr/share/{applications,icons/hicolor/22x22/apps,man/man1,nm-applet}

    cd "${pkgbase%-*}-${pkgver}"

    # Apply Ubuntu's patches
    for i in $(grep -v '#' ../debian/patches/series); do
        msg "Applying ${i} ..."
        patch -p1 -i "../debian/patches/${i}"
    done

}

build() {
    cd "${pkgbase%-*}-${pkgver}"

    autoreconf -vfi

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/networkmanager \
        --disable-static \
        --disable-maintainer-mode \
        --disable-migration \
        --with-appindicator \
        --with-wwan \
        --with-modem-manager-1

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make -j1

    cp -rf ../debian/icons/22 icons/
}

check() {
    cd "${pkgbase%-*}-${pkgver}"
    make -k check
}

package_nm-connection-editor-ubuntu() {
    pkgdesc="NetworkManager GUI connection editor"
    provides=("libnm-gtk=${pkgver}-${pkgrel}" "nm-connection-editor=${pkgver}-${pkgrel}")
    conflicts=(libnm-gtk libnm-gtk-ubuntu)
    replaces=(libnm-gtk libnm-gtk-ubuntu)

    cd "${pkgbase%-*}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    ### Split network-manager-applet
    cd ../nma
    mv "${pkgdir}"/etc/xdg/autostart etc/xdg/
    mv "${pkgdir}"/usr/bin/nm-applet usr/bin/
    mv "${pkgdir}"/usr/share/GConf usr/share/

    cd usr/share
    mv "${pkgdir}"/usr/share/applications/nm-applet.desktop applications/
    mv "${pkgdir}"/usr/share/man/man1/nm-applet.1 man/man1/
    mv "${pkgdir}"/usr/share/nm-applet/{{8021x,gsm-unlock,info}.ui,keyring.png} nm-applet/

    cd icons/hicolor/22x22
    mv "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/nm-{adhoc,mb-roam,wwan-tower}.png apps/
    mv "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/nm-{secure,vpn-active}-lock.png apps/
    mv "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/nm-{signal,tech}-*.png apps/
    mv "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/nm-*connecting*.png apps/

    # Use language packs
    rm -rf "${pkgdir}"/usr/share/locale/
}

package_network-manager-applet-ubuntu() {
    depends=(nm-connection-editor-ubuntu libmm-glib libnotify libsecret
             libappindicator-gtk3-ubuntu)
    provides=("network-manager-applet=${pkgver}")
    conflicts=(network-manager-applet)

    mv nma/* "${pkgdir}"

    # Install Ubuntu stuff
    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    install -m644 "${srcdir}"/debian/icons/22/nm-device-wired-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    install -m644 "${srcdir}"/debian/icons/22/nm-signal-00-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    ln -snf       nm-signal-00.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-none.png
    ln -snf       nm-signal-00-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-none-secure.png
    install -m644 "${srcdir}"/debian/icons/22/nm-signal-25-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    ln -snf       nm-signal-25.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-low.png
    ln -snf       nm-signal-25-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-low-secure.png
    install -m644 "${srcdir}"/debian/icons/22/nm-signal-50-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    ln -snf       nm-signal-50.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-medium.png
    ln -snf       nm-signal-50-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-medium-secure.png
    install -m644 "${srcdir}"/debian/icons/22/nm-signal-75-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    ln -snf       nm-signal-75.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-high.png
    ln -snf       nm-signal-75-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-high-secure.png
    install -m644 "${srcdir}"/debian/icons/22/nm-signal-100-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    ln -snf       nm-signal-100.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-full.png
    ln -snf       nm-signal-100-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-full-secure.png

    # Use language packs
    rm -rf "${pkgdir}"/usr/share/locale/
}
