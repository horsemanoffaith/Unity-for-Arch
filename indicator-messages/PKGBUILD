# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=zesty
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-messages
_actual_ver=13.10.1
_extra_ver=+17.04.20161201
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A place on the user's desktop that collects messages that need a response"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-messages"
license=(GPL)
depends=(accountsservice hicolor-icon-theme libindicator-gtk3-ubuntu)
makedepends=(intltool gobject-introspection gtk-doc vala)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-messages_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-autoconf-fix.patch
        0002-Disable-tests.patch)
sha512sums=('7dec0ada666d757ec748e0c1fd58037264f12275753881227660ad365f74767f35c89f806445c5d5f19a984b98b8b95588830537ef77290af80229d6b473a8a0'
            '2774bcdf64bb3723f674a8d9d10186b6c63e2adbf83b8300f44cf9a199da538b3e058a0c1b3f85eba64b18f03d3fc63424e3c23147811d16c1a7a09bef235d1c'
            '00e4aed73c5a95500c450bbdd6217bcd904df38272577eb9c496d3a19f030f4feb8346a073b9331122ff408b56c5d1e9ea99745cf56a54d6710d33aade881e2f')

prepare() {
    
    patch -p1 -i ../0001-autoconf-fix.patch
    patch -p1 -i ../0002-Disable-tests.patch
}

build() {
    
    gtkdocize
    intltoolize -f
    autoreconf -vfi
    ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static
    make
}

package() {
    
    make DESTDIR="${pkgdir}/" install

    rm -rv "${pkgdir}"/usr/share/upstart/
}
