# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Michael Healy <horsemanoffaith@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety

pkgname=unity-settings-daemon
_ubuntu_rel=0ubuntu2
_actual_ver=15.04.1
_extra_ver=+16.10.20160819
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Unity Settings Daemon"
arch=(i686 x86_64)
url="https://launchpad.net/unity-settings-daemon"
license=(GPL)
groups=(unity)
depends=(gnome-settings-daemon-ubuntu gsettings-desktop-schemas-ubuntu fcitx
         gperf hicolor-icon-theme ibus libappindicator-gtk3-ubuntu libcanberra-pulse
         libnotify librsvg libsystemd libwacom libxkbfile mesa pulseaudio
         pulseaudio-alsa upower)
makedepends=(intltool xf86-input-wacom libxslt docbook-xsl python2)
conflicts=(gnome-desktop-compat upower-compat)
#options=('!emptydirs')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-settings-daemon_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/unity-settings-daemon_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        0001-Remove-accountsservice-dependency.patch
        0002-Add-gnome-settings-daemon-3.12-rfkill-plugin.patch
        0003-Use-GNOME-3.16-deprecated-schemas.patch
        0001-usd-test-screensaver-proxy-fails-to-compile.patch)
sha512sums=('89d9dcbc5d17ccb09bc4299f869b41d8c977ba46a2766ad6b4c4e8b536ab0aba0be3adb45f0c39388f12b5f13b41a076646fd1fe3a2c5f37dc6a2e08ad2aac7f'
            'be4c7718bd4a000b61997c2385ba9d1d044523017363bdd3aac196454a3c6ddaf6e1a224a0381572923d655605f7567e7157bc379668024ddfed9719d82a0e5d'
            '2394d35355f31a1090c96a38f3e33930d70aa1683f1bc3c4b65fd6a70a9bf9e5a592109ed34dd2d5a07822b401a24d11d80a662aae850236434d89c37a9a9c89'
            '8783935c674e0b76952d9eec5f6d44c43d48a963de91cd5f5eed7cb4e09f9e86d0b516ea4f184859366ab588f5c0c68d431d9a8f35dfeab745b79e17ff711fcc'
            '9d036512105a20f1a2f045a7c6b87faedacb25748239babe3f79514c7899af74ab0e56e8d184406d183cdb2be6518f37681b03cdb6ce20901f0381b6bd0d7d68'
            '0b6fa66ebecef51c54ca04ae5aec6c6e05450668865e544f4c5ed3b142dcb22301bae440f55dfeec8fc5534a3c818d8cba4d019c32f5e3441a588bea9b93dab8')
            
prepare() {
    patch -p1 -i unity-settings-daemon_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff

    patch -p1 -i 0001-Remove-accountsservice-dependency.patch
    patch -p1 -i 0002-Add-gnome-settings-daemon-3.12-rfkill-plugin.patch
    #patch -p1 -i 0003-Use-GNOME-3.16-deprecated-schemas.patch
    patch -p1 -i 0001-usd-test-screensaver-proxy-fails-to-compile.patch
}

build() {
    autoreconf -vfi
    intltoolize -f

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/unity-settings-daemon \
        --disable-static \
        --enable-systemd \
        --enable-fcitx

    # https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    make DESTDIR="${pkgdir}/" install

    install -dm755 "${pkgdir}/usr/bin/"
    ln -s /usr/lib/unity-settings-daemon/unity-settings-daemon \
        "${pkgdir}/usr/bin/unity-settings-daemon"

    install -Dm644 debian/unity-settings-daemon.user-session.upstart \
        "${pkgdir}"/usr/share/upstart/sessions/unity-settings-daemon.conf
    install -Dm644 debian/unity-settings-daemon.user-session.desktop \
        "${pkgdir}"/usr/share/upstart/xdg/autostart/unity-settings-daemon.desktop

    # Use language packs
    rm -r "${pkgdir}/usr/share/locale/"
}
