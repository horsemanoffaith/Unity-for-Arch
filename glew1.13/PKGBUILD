# $Id: PKGBUILD 273106 2016-08-05 12:19:53Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: SleepyDog

_pkgname=glew
pkgver=1.13.0
pkgname=glew-${pkgver}
pkgrel=1
pkgdesc="The OpenGL Extension Wrangler Library"
arch=('i686' 'x86_64')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
depends=('libxmu' 'libxi' 'glu')
source=(http://downloads.sourceforge.net/${_pkgname}/${_pkgname}-${pkgver}.tgz)
sha1sums=('d5b1c499f429aa91c466193b4e8ea94a84019e37')

build() {
  cd ${_pkgname}-${pkgver}

  # move include folder to subfolder 'glew-1.13.0'
  sed -i 's:$(GLEW_DEST)/include/GL:$(GLEW_DEST)/include/glew-1.13.0/GL:' Makefile
  
  sed -i 's|lib64|lib|' config/Makefile.linux
  sed -i '/^.PHONY: .*\.pc$/d' Makefile
  make
}

package() {
  cd ${_pkgname}-${pkgver}
  # update glewmx include path to reflect previous changes (see build comments)
  msg update glewmx.pc
  sed -i 's:includedir=${prefix}/include:includedir=${prefix}/include/glew-1.13.0:' glewmx.pc

  make GLEW_DEST="${pkgdir}/usr" install.all
  
  chmod 0755 "${pkgdir}"/usr/lib/libGLEW*.so.${pkgver}
}
