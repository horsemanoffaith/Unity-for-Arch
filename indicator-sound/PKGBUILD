# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=zesty
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-sound
_actual_ver=12.10.2
_extra_ver=+17.04.20170208
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="A unified sound menu"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-sound"
license=(GPL)
depends=(accountsservice dconf gsettings-ubuntu-schemas libgee libnotify
         libpulse libxml2 pulseaudio qt5-base unity-api unity-control-center)
makedepends=(cmake gobject-introspection intltool vala)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-sound_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Not-running-on-Ubuntu-Touch.patch
        0002-Remove-tests.patch)
sha512sums=('c02a7b10c9673522402bb84d25d7fb057a2db32f075dec68a8aa2be31e92cb3688774c0bb1dff5cdc1ab09ec1dee3af6a21f9740e126965555b11b8f68a22c27'
            '3c57829cd55b1aea14b5bc2f66e8b015d28ccdd8a7d56c5d445efb61d149a49159e6be0c267d8eded5884d53361705373837cfe789d39ced9d262a5fa630491f'
            '90b70a91510d2e815739a337eaeba394b43b9954baf47ccc4efb44403b25002e5a9cf3259b043358679d70999462776c43ee72193963a4ea1e102e62ddec43a4')

prepare() {
    
    patch -p1 -i 0001-Not-running-on-Ubuntu-Touch.patch
    patch -p1 -i 0002-Remove-tests.patch
}

build() {

    [[ -d build ]] || mkdir build
    pushd build
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib
    make
    popd
}

package() {
    cd build

    make DESTDIR="${pkgdir}/" install

    rm -v "${pkgdir}"/usr/var/lib/polkit-1/localauthority/10-vendor.d/50-com.canonical.indicator.sound.AccountsService.pkla

    rm -rv "${pkgdir}"/usr/share/upstart/

}
