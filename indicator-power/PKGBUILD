# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Michael Healy <horsemanoffaith@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-power
_actual_ver=12.10.6
_extra_ver=+16.10.20160708
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="An indicator to show the power status of your devices"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-power"
license=(GPL)
depends=(dconf libnotify upower unity-control-center)
makedepends=(cmake intltool python2)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-power_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Not-a-phone.patch)
sha512sums=('639e62fdfe1b6712a107be184c789d4ba218086699bb9ffcc414d9a64bee82304455ee8421f5351bb0dcc6c022bbddf8c8183b0696b522a6a65eb00bc1838cb9'
            '82f7de1adba428bbe640e50cecdfc3d27cf13062df0739b66330e2cdf2063f9d5eef3fb4e826c5ef645356e9e4719ff3a93fe9f7d7ea93567081b47d69874c4a')
            
prepare() {
    patch -p1 -i 0001-Not-a-phone.patch

    sed -ie '/NotShowIn=/d' data/indicator-power.desktop.in
}

build() {
    export CFLAGS+=" -Wno-error"

    rm -rf build; mkdir build; cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
        -Denable_tests=OFF
    make
}

package() {
    cd build

    make DESTDIR="${pkgdir}/" install

    find "${pkgdir}" -type f -name '*.a' -delete
}
