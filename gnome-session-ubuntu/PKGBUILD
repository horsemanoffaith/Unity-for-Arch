# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname%-*}, repo=zesty
# vercheck-archlinux: name=${pkgname%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgname%-*}, majorver=3.20
# vercheck-ppa: name=${pkgname%-*}, url=ppa:gnome3-team/gnome3-staging

pkgname=gnome-session-ubuntu
_use_ppa=false
_ppa_rel=1ubuntu1~ubuntu16.10.1
_ppa_ver=3.22.2
_ubuntu_rel=1ubuntu1
pkgver=3.22.2
pkgrel=1
pkgdesc="The GNOME Session Handler"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(dconf gnome-desktop gsettings-desktop-schemas-ubuntu gtk3-ubuntu json-glib libgl
	libsm libxtst polkit-gnome systemd)
makedepends=(gnome-common gtk-doc intltool mesa python xtrans)
groups=(gnome unity)
provides=("gnome-session=${pkgver}")
conflicts=(gnome-session)
options=(!emptydirs)
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        polkit-gnome-authentication-agent-1-unity.desktop)

if [[ "${_use_ppa}" == "true" ]]; then
    source+=("https://launchpad.net/~gnome3-team/+archive/ubuntu/gnome3-staging/+files/gnome-session_${_ppa_ver:-${pkgver}}-${_ppa_rel}.debian.tar.xz")
else
    source+=("https://launchpad.net/ubuntu/+archive/primary/+files/gnome-session_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
fi

sha512sums=('2c84a3a791d740c8edc2af96e8ed0b56f56dfac20fde6e8bc23a7aa75c0a004e98fcb9bb85b043ed3b1cd57cdc889348c461d9536cd2631dd53d8a0d6f8e2acf'
            '34e32d1c3fd7c417b5dc70737b9dff9c9b0b660a6c99cb8c29e944818df0cb643fcac01c6c5252849bb8595a348aaf4d2a24985560c6b8a9653544e146e5b4af'
            'f5345f732bbb859658e4d0257dfd51bbc40e8ded4d61862644a2651fe958624fc13933f76c99ddee157c237e4b0fe2de4026c76294009de5f8f42b38089d62a1')

prepare() {
    cd "${pkgname%-*}-${pkgver}"

    sed -i '/53_add_sessionmigration.patch/d' ../debian/patches/series

   for i in $(grep -v '#' ../debian/patches/series); do
        msg "Applying ${i} ..."
        patch -p1 -i "../debian/patches/${i}"
     done
}

build() {
    cd "${pkgname%-*}-${pkgver}"

    autoreconf -vfi

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/gnome-session \
        --disable-schemas-compile \
        --enable-systemd \
        --disable-gconf

    make
}

package() {
    cd "${pkgname%-*}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    # polkit
    install -dm755 "${pkgdir}/etc/xdg/autostart/"
    install -m644 "${srcdir}/polkit-gnome-authentication-agent-1-unity.desktop" \
        "${pkgdir}/etc/xdg/autostart/"

    # LightDM
    install -dm755 "${pkgdir}/usr/share/lightdm/lightdm.conf.d/"
    install -m644 "${srcdir}/debian/data/50-ubuntu.conf" \
        "${pkgdir}/usr/share/lightdm/lightdm.conf.d/"

    # Upstart
    install -Dm644 "${srcdir}/debian/gnome-session-bin.user-session.upstart" \
        "${pkgdir}/usr/share/upstart/sessions/gnome-session.conf"

    install -dm755 "${pkgdir}/usr/share/upstart/systemd-session/upstart/"
    install -m644 "${srcdir}/debian/data/gnome-session.override" \
        "${pkgdir}/usr/share/upstart/systemd-session/upstart/"

    # systemd
    install -dm755 "${pkgdir}/usr/lib/systemd/user/"
    install -m644 \
        "${srcdir}/debian/data/gnome-session.service" \
        "${srcdir}/debian/data/ubuntu-session.target" \
        "${pkgdir}/usr/lib/systemd/user/"

    install -dm755 "${pkgdir}/usr/lib/gnome-session/"
    install -m755 "${srcdir}/debian/data/run-systemd-session" \
        "${pkgdir}/usr/lib/gnome-session/"

    rm -rv "${pkgdir}"/usr/share/upstart

}
